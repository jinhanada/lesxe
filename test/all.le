(demac (test name . body)
  `(catch
    (let ()
      (prin1 (str 'test " " ',name " ... "))
      ,@body
      (print 'ok))
    (fn (e) (print (list 'test 'failed ',name e)))))

(de (assert cond)
  (or cond (raise 'assert)))

(demac (assert= a b)
  (let ((x (sym:gen))
        (y (sym:gen)))
    `(let ((,x ,a)
           (,y ,b))
       (or (= ,x ,y)
           (raise (list 'assert ',a '= ',b 'but 'got ,x 'and ,y))))))

(demac (assert-equal a b)
  (let ((x (sym:gen))
        (y (sym:gen)))
    `(let ((,x ,a)
           (,y ,b))
       (or (equal? ,x ,y)
           (raise (list 'assert ',a 'equals ',b 'but 'got ,x 'and ,y))))))


(test letrec
  ;; single recursion
  (assert
   (= 11
      (letrec ((f (fn (x)
                    (if (> x 10)
                      x
                      (f (+ x 1))))))
        (f 0))))
  
  ;; mutual recursion
  (assert
   (= 11
      (letrec ((f (fn (x) (if (> x 10) x (g (+ x 1)))))
               (g (fn (x) (if (> x 10) x (f (+ x 1))))))
       (f 0)))))


(test record-case
  (assert=
   'a
   (record-case '(foo a b)
     ((foo a b) a)
     (else      'b)))

  (assert=
   'b
   (record-case '(bar a b)
     ((foo a b) a)
     (else      'b)))

  (assert=
   'x
   (catch
    (record-case '(bar a b)
      ((foo a b) a))
    (fn (_) 'x))))


(test cond
  (letrec
      ((f (n)
          (cond
           ((= n 1) 'a)
           ((= n 2) 'b)
           (else    'c))))
    (assert= 'a (f 1))
    (assert= 'b (f 2))
    (assert= 'c (f 3))))


(test threading-macro
  (assert= 2 (-> 1 inc))
  (assert= 1 (-> 3 (- 2)))
  (assert= 3 (-> 1 (+ 2) (- 2) inc inc)))


(de (internal-test)
  (de (a n)
    (de (a2)
      (if (> n 10) n (b (+ n 1))))
    (a2))
  (de (b n) (if (> n 10) n (a (+ n 1))))
  (a 1))

(test internal-de
  (assert= 11 (internal-test)))


(test match1
  ;; number
  (assert= 5 (match '(1 2 3) ((1 a b) (+ a b))))

  ;; quote
  (assert= 5 (match '(a 2 3) (('a b c) (+ b c))))
  (assert= 5 (match 'a ('a 5)))
  (assert= 5
           (match 'a
             (('a) 6)
             ('a   5)))

  ;; rest
  (assert= 5
           (match '(1 2 3)
             ((1 . xs) (+ (car xs) (second xs)))))

  ;; ?
  (assert= 5
           (match '(a 2 3)
             (((? symbol?) b c) (+ b c))))

  ;; nested
  (assert= 10
           (match '((1 2) 3 (4))
             (((a b) c (d))
              (+ a b c d))))
  )


(test list:equal?
  (assert= true (list:equal? '(a b c) '(a b c)))
  (assert= true (list:equal? '(a) '(a))))


(test list:remove
  (assert-equal '(a b c) (list:remove '(a b c)   'x))
  (assert-equal '(a b c) (list:remove '(a b x c) 'x))
  (assert-equal '(a b c) (list:remove '(x a b c) 'x))
  )



(defrecord <foo> a b c)

(test defrecord
  (let ((r (<foo> 1 2 3)))
    (<foo>-a! r (inc (<foo>-a r) 1))
    (<foo>-b! r (inc (<foo>-b r) 1))
    (<foo>-c! r (inc (<foo>-c r) 1))
    (assert-equal '(2 3 4)
                  (list (<foo>-a r)
                        (<foo>-b r)
                        (<foo>-c r)))))
